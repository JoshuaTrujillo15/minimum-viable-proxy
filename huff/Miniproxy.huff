// EIP1967 PROXY CONTRACT

// -------------------------------------------------------------------------------------------------
// CONSTANTS

// uint256(keccak256("eip1967.proxy.implementation")) - 1
#define constant PROXY_SLOT = 0x360894a13ba1a3210667c828492db98dca3e2076cc3735a920a3ca505d382bbc

// placeholder factory address
#define constant FACTORY_PLACEHOLDER = 0xffffffffffffffffffffffffffffffffffffffff

#define constant ONE = 0x01

#define constant WORD_SIZE = 0x20

#define constant SELECTOR_LEN = 0x04

#define macro CONSTRUCTOR() = takes (0) returns (0) {
    // STORE IMPLEMENTATION
    [WORD_SIZE]     // [word]
    dup1            // [word, word]
    codesize        // [codesize, word, word]
    sub             // [impl_offset, word]
    returndatasize  // [zero, impl_offset, word]
    codecopy        // []
    returndatasize  // [zero]
    mload           // [impl]
    [PROXY_SLOT]    // [proxy_slot]
    sstore          // []

    // LOAD RUNTIME INTO MEMORY
    initcode_end    // [initcode_end]
    codesize        // [codesize, initcode_end]
    sub             // [runtime_offset]
    initcode_end    // [initcode_end, runtime_offset]
    returndatasize  // [zero, initcode_end, runtime_offset]
    codecopy        // []

    // REPLACE FACTORY PLACEHOLDER WITH CALLER
    caller          // [factory]
    initcode_end    // [initcode_end, factory]
    [ONE]           // [one, initcode_end, factory]
    add             // [factory_placeholder_offset, factory]
    mstore          // []

    // RETURN
    msize           // [size]
    returndatasize  // [offset, size]
    return          // []

    // used to calculate runtime offsets
    initcode_end:
}

#define macro MAIN() = takes (0) returns (0) {
    // CHECK IF CALLER IS FACTORY
    [FACTORY_PLACEHOLDER]   // [factory]
    caller                  // [caller, factory]
    eq                      // [is_factory]
    is_factory              // [is_factory_dest, is_factory]
    jumpi                   // []

    // COPY CALLDATA TO MEMORY
    calldatasize            // [calldatasize]
    returndatasize          // [zero, calldatasize]
    returndatasize          // [zero, zero, calldatasize]
    calldatacopy            // []

    // DELEGATECALL
    returndatasize          // [retsize]
    returndatasize          // [retoffset, retsize]
    calldatasize            // [argsize, retoffset, retsize]
    returndatasize          // [argoffset, argsize, retoffset, retsize]
    [PROXY_SLOT]            // [proxy_slot, argoffset, argsize, retoffset, retsize]
    sload                   // [impl, argoffset, argsize, retoffset, retsize]
    gas                     // [gas, impl, argoffset, argsize, retoffset, retsize]
    delegatecall            // [success]

    // COPY RETURNDATA TO MEMORY
    returndatasize          // [retsize, success]
    0x00                    // [retoffset, retsize, success]
    dup1                    // [memoffset, retoffset, retsize, success]
    returndatacopy          // [success]

    // RETURN IF SUCCESS, ELSE BUBBLE UP ERROR
    call_success            // [call_success, success]
    jumpi                   // []

    // FAILED
    returndatasize          // [retsize]
    0x00                    // [zero, retsize]
    revert                  // []

    is_factory:             // []
        returndatasize      // [impl_offset]
        calldataload        // [impl_contract]
        [PROXY_SLOT]        // [proxy_slot, impl_contract]
        sstore              // []
        stop                // []

    // SUCCESS
    call_success:
        returndatasize      // [retsize]
        0x00                // [zero, retsize]
        return              // []
}
